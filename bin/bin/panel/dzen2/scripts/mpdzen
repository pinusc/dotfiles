#!/bin/zsh
source ~/bin/lib/colors.sh

RESPATH=( $0(:A:h) )

FN_ARTIST='-*-gohufont-*-*-*-*-14-*-*-*-*-*-*-*'
CL_SONG=$c_red
FN_SONG='-*-gohufont-*-*-*-*-14-*-*-*-*-*-*-*'
CL_ARTIST=$c_white
FN_ALBUM='-*-gohufont-*-*-*-*-11-*-*-*-*-*-*-*'
CL_ALBUM=$c_magenta

FN_TIME='-*-gohufont-*-*-*-*-11-*-*-*-*-*-*-*'
CL_TIME=$c_yellow

FN_INFO='-*-gohufont-*-*-*-*-11-*-*-*-*-*-*-*'
CL_INFO=''
CL_BAR=$c_blue
CL_BAR_UNDONE=$base03
CL_COVER=$base00


[[ -n $commands[dzenlines] ]] && dzen=$commands[dzenlines] || dzen=dzen2

# get some params
self=( $0(:A:h) )
[[ -e $self/mpc_parse ]] || { echo 'Missing mpc_parse!'; return 1 }
# note mpc_parse uses $1 as hostname
. ${self}/mpc_parse

# make sure the parsing succeeded - we have no proper data otherwise
[[ $? != 0 ]] && return 1

AW=$(textwidth "$FN_ARTIST" "$ARTIST")
while [[ $AW -gt 292 ]]; do
    ARTIST=$ARTIST[1,-4]'..'
    AW=$(textwidth "$FN_ARTIST" "$ARTIST")
done

SW=$(textwidth "$FN_SONG" "$SONG")
while [[ $SW -gt 310 ]]; do
    SONG=$SONG[1,-4]'..'
    SW=$(textwidth "$FN_SONG" "$SONG")
done

W=360

# dynamic width
fixw=90
(( $AW+$fixw > $W )) && W=$[ $fixw + $AW ]
(( $SW+$fixw > $W )) && W=$[ $fixw + $SW ]

# center x under the mouse
x=`xdotool getmouselocation | egrep -oh "[0-9]+" | head -n 1`
x=$((x - W / 2))
if [ $x -le 0 ]; then
    x=5
fi
# y is height of bar + 10
y=40

# any cover here? match case-insensitively and allow two errors in the name.
setopt extended_glob
img=( covers/(#ia3)${~ARTIST}/${~${(q)ALBUM}:gs/\//_}.xpm(N) )
[[ -z $img ]] && img=( ~/.cache/covers/(#ia3)${~ARTIST}/${~${ALBUM}:gs/\//_}.xpm(N) )
#if there isn't a cover image for album at least for artist?
[[ -z $img ]] && img=( ~/.cache/covers/(#ia3)${~ARTIST}.xpm(N) )
if [[ -n $img ]]; then
    img=${${img[1]}:gs/)/\\)}
    img="^i($img)"
else
    img="^fg($CL_COVER)^r(64x64)^pa(36;28)^fg($c_bright_black)?"
fi

case $MPDSTATE in
    #playing) STATE="^fg(#$c_white)^i($RESPATH/play.xbm) " ;;
    playing) STATE="^fg(#$base05)^fn(FontAwesome)\uf04b^fn()^fg() " ;;
     paused) STATE="^fg(#$c_white)^fn(FontAwesome)\uf04c^fn()^fg() " ;;
esac

# Don't repeat yourself
# It's written as a function because width varies
dcommand () {
    $dzen -p 3 -h 72 -x $x -y $y -w $W -ta l -e "button1=exit"
}

# long interpret or song title? switch layout then.
if [ $AW -ge 220 ] || [ $SW -ge 220 ]; then
    [ $W -ge 395 ] && W=395
    # this one moves 
    echo "^ib(1)" \
    "^pa(8;4)$img" \
    "^pa(80;8)$STATE" \
    "^pa(98;4)^fg($CL_ARTIST)^fn($FN_ARTIST)$ARTIST" \
    "^pa(80;28)^fg($CL_SONG)^fn($FN_SONG)$SONG" \
    "^pa(80;49)^fg($CL_BAR)^r(200x6)^pa(80;49)^fg($CL_BAR)^r($(($PERCENT*2))x6)" \
    "^pa(300;48)^fg($CL_TIME)^fn($FN_TIME)$TIME_NOW / $TIME_TOTAL" \
    "^pa(365;54)^fg($CL_INFO)^fn($FN_INFO)$PL_NOW / $PL_TOTAL" \
    "^pa(80;58)^fg($CL_INFO)^fn($FN_INFO)$STATUS" | dcommand
else
    echo "^ib(1)" \
    "^pa(8;4)$img" \
    "^pa(80;8)$STATE" \
    "^pa(98;4)^fg($CL_SONG)^fn($FN_SONG)$SONG" \
    "^pa($(($W-30));35)^fg($CL_INFO)^fn($FN_INFO)$PL_NOW / $PL_TOTAL" \
    "^pa(80;28)^fg($CL_ARTIST)^fn($FN_ARTIST)$ARTIST" \
    "^pa(80;49)^fg($CL_BAR_UNDONE)^r(200x6)^pa(80;49)^fg($CL_BAR)^r($(($PERCENT*2))x6)" \
    "^pa(300;48)^fg($CL_TIME)^fn($FN_TIME)$TIME_NOW / $TIME_TOTAL" \
    "^pa(80;58)^fg($CL_INFO)^fn($FN_INFO)$STATUS" | dcommand
fi

# couldn't fit this in the layout..
# "^pa(260;12)^fg($CL_ALBUM)^fn($FN_ALBUM)$ALBUM"

